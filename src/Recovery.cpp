#include <Arduino.h>
#include <LittleFS.h>
#include <nvs_flash.h>
#include <Preferences.h>
#include <sdkconfig.h>
#include <WiFi.h>
#include "Recovery.h"
#include "Somfy.h"
#include "ConfigSettings.h"

extern ConfigSettings settings;
extern SomfyShadeController somfy;

bool _pendingNetSecuRecovery = false;
bool _pendingFactory = false;

void visualFeedback(int durationMs, int speedMs) {
  if (LED_PIN == -1) { delay(durationMs); return; }
  unsigned long start = millis();
  while (millis() - start < durationMs) {
    digitalWrite(LED_PIN, !digitalRead(LED_PIN));
    delay(speedMs);
  }
}

void handlePowerCycleReset() {
  if (LED_PIN != -1) {
    pinMode(LED_PIN, OUTPUT);
    digitalWrite(LED_PIN, LED_OFF); // Éteinte au départ
  }

  Preferences p;
  p.begin("rst_logic", false);
  int count = p.getInt("c", 0) + 1;
  p.putInt("c", count);
  p.end();

  Serial.print(F("\n[BOOT] Cycle: "));
  Serial.println(count);

  int flashSpeed = 0;
  if (count == NET_RECOVERY_CYCLES) flashSpeed = 150;
  else if (count >= FULL_FACTORY_CYCLES) flashSpeed = 50;

  unsigned long startWait = millis();
  while (millis() - startWait < BOOT_TIMEOUT) {
    if (LED_PIN != -1) {
      if (flashSpeed > 0) {
        digitalWrite(LED_PIN, !digitalRead(LED_PIN)); // Clignotement de notification
        delay(flashSpeed);
      } else {
        digitalWrite(LED_PIN, LED_ON); // Reste allumée fixe (normal)
        delay(100);
      }
    } else {
      delay(100);
    }
  }

  p.begin("rst_logic", false);
  p.putInt("c", 0);
  p.end();

  // Éteindre la LED après la phase de boot timeout
  if (LED_PIN != -1) digitalWrite(LED_PIN, LED_OFF);

  if (count >= FULL_FACTORY_CYCLES) {
    _pendingFactory = true;
    Serial.println(F("Pending: Factory Reset"));
  }
  else if (count == NET_RECOVERY_CYCLES) {
    _pendingNetSecuRecovery = true;
    Serial.println(F("Pending: Network Recovery"));
  }

  Serial.println(F("Boot OK"));
}

void resetAccessAndNetworkConfig() {
  if (!_pendingNetSecuRecovery) return;

  Serial.println(F("\n[ACTION] Resetting Net & Secu..."));
  if (LED_PIN != -1) {
    for (int i = 0; i < 20; i++) { digitalWrite(LED_PIN, !digitalRead(LED_PIN)); delay(50); }
  }

  settings.connType = conn_types_t::unset;
  memset(settings.WIFI.ssid, 0, sizeof(settings.WIFI.ssid));
  memset(settings.WIFI.passphrase, 0, sizeof(settings.WIFI.passphrase));
  settings.IP.dhcp = true;
  settings.Security.type = security_types::None;
  memset(settings.Security.username, 0, sizeof(settings.Security.username));
  memset(settings.Security.password, 0, sizeof(settings.Security.password));
  memset(settings.Security.pin, 0, sizeof(settings.Security.pin));
  settings.Security.permissions = 0;

  settings.save();
  settings.WIFI.save();
  settings.IP.save();
  settings.Security.save();

  // Éteindre proprement la LED avant reboot
  if (LED_PIN != -1) digitalWrite(LED_PIN, LED_OFF);

  WiFi.disconnect(true, true);
  Serial.println(F("Rebooting..."));
  delay(1000);
  ESP.restart();
}

void performFactoryReset() {
  Serial.println(F("\n[CRITICAL] Factory Resetting..."));
  if (LED_PIN != -1) {
    for (int i = 0; i < 40; i++) { digitalWrite(LED_PIN, !digitalRead(LED_PIN)); delay(30); }
  }
  somfy.end();
  nvs_flash_erase();
  nvs_flash_init();
  const char* targets[] = {"/shades.cfg", "/shades.cfg.bak", "/shades.tmp", "/controller.backup"};
  for (const char* t : targets) {
    if (LittleFS.exists(t)) LittleFS.remove(t);
  }

  // Éteindre proprement la LED avant reboot
  if (LED_PIN != -1) digitalWrite(LED_PIN, LED_OFF);

  Serial.println(F("Success. Rebooting."));
  delay(2000);
  ESP.restart();
}
